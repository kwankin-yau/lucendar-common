plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.lucendar'
version = '2.0.0'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = '8'
targetCompatibility = '8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-collections:commons-collections:3.2.2'
    implementation('com.google.code.gson:gson:2.10.1')

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Lucendar common library'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'A common library used in other open source projects.'
                url = 'https://github.com/kwankin-yau/lucendar-common'

                scm {
                    connection = 'https://github.com/kwankin-yau/lucendar-common.git'
                    developerConnection = 'https://github.com/kwankin-yau/lucendar-common.git'
                    url = 'https://github.com/kwankin-yau/lucendar-common'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'kky'
                        name = 'kwankin-yau'
                        email = 'alphax@vip.163.com'
                    }
                }
            }
        }

    }
}


javadoc {
    options.encoding = 'UTF-8'

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign publishing.publications.maven
}

