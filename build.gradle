plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.lucendar'
version = '2.0.0'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = '8'
targetCompatibility = '8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-collections:commons-collections:3.2.2'
    implementation('com.google.code.gson:gson:2.10.1')

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    repositories {
        maven {
            name = "my-nuxus"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                name = "snapshots"
                url = "${snapshotUrl}"
            } else {
                name = "releases"
                url = "${releaseUrl}"
            }

            credentials {
                username = "${ossUser}"
                password = "${ossPwd}"
            }
        }

        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}


javadoc {
    options.encoding = 'UTF-8'

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign configurations.archives
}
